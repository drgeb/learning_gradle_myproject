def isReleaseVersion = !(rootProject.version.endsWith('-SNAPSHOT'))
def publishSignatureRequired = 'true' == rootProject.findProperty('publishSignatureRequired') &&
                               'true' != System.getenv('JITPACK') &&
                               !rootProject.hasProperty('nosign')
def publishUrlForRelease = rootProject.findProperty('publishUrlForRelease')
def publishUrlForSnapshot = rootProject.findProperty('publishUrlForSnapshot')
def publishUsernameProperty = rootProject.findProperty('publishUsernameProperty')
def publishPasswordProperty = rootProject.findProperty('publishPasswordProperty')

def signingKeyId = rootProject.findProperty("signingKeyId")
def signingKey = rootProject.findProperty("signingKey")
def signingPassword = rootProject.findProperty("signingPassword")

if (publishSignatureRequired) {
    apply plugin: 'signing'
}

rootProject.ext {
    isPublishing = { gradle.taskGraph.allTasks.find {
        it.name =~ /(?:^|:)publish[^:]*ToSonatype[^:]*$/ } != null
    }
    if (publishSignatureRequired) {
        isSigning = {
            (rootProject.signing.signatory != null || signingKey != null) &&
            (isReleaseVersion || rootProject.hasProperty('sign'))
        }
    } else {
        isSigning = { false }
    }

    if (publishSignatureRequired) {
        gradle.taskGraph.whenReady {
            // Do *NOT* proceed if a user is publishing a release version and signatory is missing.
            if (rootProject.ext.isPublishing() && isReleaseVersion &&
                rootProject.signing.signatory == null && signingKey == null) {

                throw new IllegalStateException(
                        'Cannot publish a release version without a GPG key; missing signatory. ' +
                        'Use "-Pnosign" option to disable PGP signing.')
            }
        }
    }
}

subprojects {
    ext {
        isPublishing = rootProject.ext.isPublishing
        isSigning = rootProject.ext.isSigning
    }
}

repositories {
    maven {
    name 'nexus'
    url "http://[your nexus ip]:[your nexus port]/repository/maven-releases/"
    credentials {
        username rootProject.findProperty('repoUser')
        password rootProject.findProperty('repoPassword')
}}}

configure(projectsWithFlags('publish')) {
    apply plugin: 'maven-publish'
    // apply plugin: 'io.github.gradle-nexus.publish-plugin'

    if (project.ext.isSigning()) {
        apply plugin: 'signing'

        signing {
            if (System.getenv("CI") != null && signingKey != null) {
                useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
            }
            required { true }
        }
    }

    task install(
            group: 'Publishing',
            description: 'An alias of publishToMavenLocal',
            dependsOn: tasks.publishToMavenLocal)
}
